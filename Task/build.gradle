plugins {
    id 'java'
    id 'jacoco'
    id 'jacoco-report-aggregation'
    id 'application'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'java-test-fixtures'
}

group = 'com.demo'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

repositories {
    mavenCentral()
}

dependencyManagement {
    imports {
        mavenBom 'org.springframework:spring-framework-bom:6.2.5'
    }
}

dependencies {
    // Lombok
    compileOnly 'org.projectlombok:lombok:1.18.36'
    annotationProcessor 'org.projectlombok:lombok:1.18.36'

    // Spring Framework Core, Web Components and AOP
    implementation 'org.springframework:spring-context'
    implementation 'org.springframework:spring-web'
    implementation 'org.springframework:spring-webmvc'
    implementation 'org.springframework:spring-aop'
    implementation 'org.springframework:spring-aspects'
    implementation 'org.springframework:spring-core'
    implementation 'org.springframework:spring-beans'

    // Jakarta Servlet API
    implementation group: 'jakarta.servlet', name: 'jakarta.servlet-api', version: '6.0.0'

    // Jetty - Embedded Web Server
    implementation group: 'org.eclipse.jetty', name: 'jetty-server', version: '11.0.25'
    implementation group: 'org.eclipse.jetty', name: 'jetty-servlet', version: '11.0.25'
    implementation group: 'org.eclipse.jetty', name: 'jetty-webapp', version: '11.0.25'
    implementation group: 'org.eclipse.jetty', name: 'jetty-annotations', version: '11.0.25'

    // Database - PostgreSQL Driver & Liquibase for Migrations
    implementation group: 'org.postgresql', name: 'postgresql', version: '42.7.5'
    implementation group: 'org.liquibase', name: 'liquibase-core', version: '4.31.1'

    // Jackson - JSON Processing & YAML Support
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.18.3'
    implementation group: 'com.fasterxml.jackson.dataformat', name: 'jackson-dataformat-yaml', version: '2.18.3'
    implementation group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jsr310', version: '2.18.3'
    implementation group: 'org.yaml', name: 'snakeyaml', version: '2.4'

    // MapStruct - Object Mapping
    implementation group: 'org.mapstruct', name: 'mapstruct', version: '1.6.3'
    annotationProcessor group: 'org.mapstruct', name: 'mapstruct-processor', version: '1.6.3'

    // Testing - JUnit, Mockito, Testcontainers, and Spring Test
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.10.1'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.10.1'
    testImplementation 'org.mockito:mockito-core:5.10.0'
    testImplementation 'org.mockito:mockito-junit-jupiter:5.10.0'
    testImplementation 'org.assertj:assertj-core:3.24.2'
    testImplementation 'org.springframework:spring-test'
    testImplementation group: 'com.jayway.jsonpath', name: 'json-path', version: '2.9.0'

    // Testcontainers - Database Testing with PostgreSQL
    testImplementation group: 'org.testcontainers', name: 'testcontainers', version: '1.20.6'
    testImplementation group: 'org.testcontainers', name: 'junit-jupiter', version: '1.20.6'
    testImplementation group: 'org.testcontainers', name: 'postgresql', version: '1.20.6'

    // Swagger
    implementation group: 'org.springdoc', name: 'springdoc-openapi-starter-webmvc-ui', version: '2.8.6'
    implementation group: 'org.springdoc', name: 'springdoc-openapi-starter-webmvc-api', version: '2.8.6'
    implementation group: 'org.webjars', name: 'swagger-ui', version: '5.20.1'
    implementation group: 'org.webjars', name: 'webjars-locator-core', version: '0.59'

    // Jetty logging
    implementation group: 'org.slf4j', name: 'slf4j-api', version: '2.0.17'
    implementation group: 'org.slf4j', name: 'slf4j-simple', version: '2.0.17'
}

application {
    mainClass = 'com.demo.finance.app.TaskMain'
    applicationDefaultJvmArgs = [
            '-Dorg.eclipse.jetty.server.webapp.ContainerIncludeJarPattern=.*',
            '-Dorg.eclipse.jetty.servlet.Default.useFileMappedBuffer=false',
            '-Djava.util.logging.config.file=/app/logging.properties'
    ]
}

distributions {
    main {
        contents {
            from('src/main/resources') {
                into 'resources'
            }
            from(configurations.runtimeClasspath) {
                into 'lib'
                duplicatesStrategy = DuplicatesStrategy.EXCLUDE
            }
        }
    }
}

sourceSets {
    main {
        java {
            srcDirs = ['src/main/java']
        }
        resources {
            srcDirs = ['src/main/resources']
        }
    }
    test {
        java {
            srcDirs = ['src/test/java']
        }
        resources {
            srcDirs = ['src/test/resources']
        }
    }
    integrationTest {
        java {
            srcDirs = ['src/test/java']
        }
        resources {
            srcDirs = ['src/test/resources']
        }
    }
}

tasks.named('test') {
    useJUnitPlatform()
    filter {
        excludeTestsMatching "com.demo.finance.app.TaskMainTest"
        excludeTestsMatching "com.demo.finance.app.config.LiquibaseManagerTest"
        excludeTestsMatching "com.demo.finance.app.config.DataSourceManagerTest"
        excludeTestsMatching "com.demo.finance.out.repository.impl.BudgetRepositoryImplTest"
        excludeTestsMatching "com.demo.finance.out.repository.impl.TransactionRepositoryImplTest"
        excludeTestsMatching "com.demo.finance.out.repository.impl.UserRepositoryImplTest"
        excludeTestsMatching "com.demo.finance.out.repository.impl.GoalRepositoryImplTest"
    }
    maxParallelForks = Runtime.runtime.availableProcessors()
    finalizedBy jacocoTestReport
}

tasks.register('integrationTest', Test) {
    useJUnitPlatform()
    filter {
        includeTestsMatching "com.demo.finance.app.TaskMainTest"
        includeTestsMatching "com.demo.finance.app.config.LiquibaseManagerTest"
        includeTestsMatching "com.demo.finance.app.config.DataSourceManagerTest"
        includeTestsMatching "com.demo.finance.out.repository.impl.BudgetRepositoryImplTest"
        includeTestsMatching "com.demo.finance.out.repository.impl.TransactionRepositoryImplTest"
        includeTestsMatching "com.demo.finance.out.repository.impl.UserRepositoryImplTest"
        includeTestsMatching "com.demo.finance.out.repository.impl.GoalRepositoryImplTest"
    }
    maxParallelForks = 1
    forkEvery = 1
    systemProperty "junit.jupiter.execution.parallel.enabled", "false"
    finalizedBy jacocoTestReport
    jacoco {
        enabled = true
    }
}

tasks.named('jacocoTestReport', JacocoReport) {
    dependsOn tasks.named('test'), tasks.named('integrationTest')
    executionData.setFrom(fileTree(layout.buildDirectory.dir("jacoco")).include(
            "test.exec",
            "integrationTest.exec"
    ))
    sourceDirectories.setFrom(files(sourceSets.main.allSource.srcDirs))
    classDirectories.setFrom(files(sourceSets.main.output))
    reports {
        xml.required.set(true)
        html.required.set(true)
    }
}

tasks.named('jar') {
    manifest {
        attributes(
                'Main-Class': 'com.demo.finance.app.TaskMain',
                'Class-Path': configurations.runtimeClasspath.collect { it.name }.join(' ')
        )
    }
    into('lib') {
        from configurations.runtimeClasspath
    }
}

jacoco {
    toolVersion = "0.8.12"
}

configurations {
    integrationTestImplementation.extendsFrom testImplementation
    integrationTestRuntimeOnly.extendsFrom testRuntimeOnly
}